apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "se.qxx.android.fiatlux"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        setProperty("archivesBaseName", "fiatlux-$versionName")
    }

    signingConfigs {
        release {
				if (project.hasProperty("RELEASE_STORE_FILE")) {
	            storeFile file(RELEASE_STORE_FILE)
				}

				if (project.hasProperty("RELEASE_STORE_PASSWORD")) {
	            storePassword RELEASE_STORE_PASSWORD
				}

				if (project.hasProperty("RELEASE_KEY_ALIAS")) {
	            keyAlias RELEASE_KEY_ALIAS	
				}

				if (project.hasProperty("RELEASE_KEY_PASSWORD")) {
	            keyPassword RELEASE_KEY_PASSWORD
				}
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.txt'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

}

def publish = project.tasks.create("publishAll");
if (project.hasProperty("RELEASE_DEPLOY_PATH")) {
    def deployPath = project.properties["RELEASE_DEPLOY_PATH"];
    def folder = new File(deployPath)

    if (folder.exists()) {
        android.applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def task = project.tasks.create("publish" + variant.name.capitalize() + "Apk", Copy)

                task.from(output.outputFile)
                task.into(folder)

                task.dependsOn variant.assemble
                publish.dependsOn task
            }
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile project(':se.qxx.fiatlux.domain')
    compile 'se.qxx:android-tools:1.0.2'
    compile 'com.google.protobuf:protobuf-java:2.4.1'
    compile 'com.googlecode:protobuf-socket-rpc:2.0'
}


